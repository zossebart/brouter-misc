---context:global   # following code refers to global config

# the elevation parameters

assign downhillcost 0
assign downhillcutoff 1.5
assign uphillcost 0
assign uphillcutoff 1.5

assign processUnusedTags 1

assign   turnInstructionMode  = 1  # %turnInstructionMode% | Mode for the generated turn instructions | [0=none, 1=auto-choose, 2=locus-style, 3=osmand-style, 4=comment-style, 5=gpsies-style, 6=orux-style, 7=locus-old-style]
assign   validForFoot        1

---context:way   # following code refers to way-tags

assign turncost 0

assign initialclassifier =
     if route=ferry then 1
     else 0

assign initialcost switch route=ferry 10000 0

#
# implicit access here just from the motorroad tag
# (implicit access rules from highway tag handled elsewhere)
#
assign defaultaccess
       switch access=
              not motorroad=yes
              switch or access=private access=no
                     0
                     1

#
# calculate logical foot access
#
assign footaccess
       switch foot=
              defaultaccess
              not or foot=private foot=no

assign accesspenalty switch footaccess 0 100000

assign isresidentialorliving = or highway=residential|living_street living_street=yes

assign costfactor
  add accesspenalty

  if      and highway= not route=ferry then  100000

  else if route=ferry                  then  5.67
  else if or isresidentialorliving
          highway=footway|pedestrian|platform|corridor|crossing|bus_stop then 1
  else if highway=track then (
              if tracktype=grade1 then 1
              else if tracktype=grade2 then 1.5
              else if tracktype=grade3 then 1.7
              else if tracktype=grade4 then 2.5
              else 5
       )
  else if highway=service|rest_area|tertiary|tertiary_link|unclassified then 2
  else if highway=services|yes|secondary|secondary_link|road then 3
  else if highway=primary|primary_link then 4
  else 99999

# way priorities used for voice hint generation

assign priorityclassifier =

  if      ( highway=motorway                       ) then  30
  else if ( highway=motorway_link                  ) then  29
  else if ( highway=trunk                          ) then  28
  else if ( highway=trunk_link                     ) then  27
  else if ( highway=primary                        ) then  26
  else if ( highway=primary_link                   ) then  25
  else if ( highway=secondary                      ) then  24
  else if ( highway=secondary_link                 ) then  23
  else if ( highway=tertiary                       ) then  22
  else if ( highway=tertiary_link                  ) then  21
  else if ( highway=unclassified                   ) then  20
  else if ( isresidentialorliving                  ) then  6
  else if ( highway=service                        ) then  6
  else if ( highway=cycleway                       ) then  6
  else if ( or bicycle=designated bicycle_road=yes ) then  6
  else if ( highway=track                          ) then if tracktype=grade1 then 6 else 4
  else if ( highway=bridleway|road|path|footway    ) then  4
  else if ( highway=steps                          ) then  2
  else if ( highway=pedestrian                     ) then  2
  else 0

# some more classifying bits used for voice hint generation...

assign isroundabout = junction=roundabout
assign islinktype = highway=motorway_link|trunk_link|primary_link|secondary_link|tertiary_link
assign isgoodforcars = if greater priorityclassifier 6 then true
                  else if ( or isresidentialorliving highway=service ) then true
                  else if ( and highway=track tracktype=grade1 ) then true
                  else false


# ... encoded into a bitmask

assign classifiermask add multiply isroundabout   4
                      add multiply islinktype     8
                          multiply isgoodforcars 16

---context:node  # following code refers to node tags

assign defaultaccess
       switch access=
              1 # add default barrier restrictions here!
              switch or access=private access=no
                     0
                     1

assign footaccess
       switch foot=
              defaultaccess
              switch or foot=private foot=no
                     0
                     1

assign initialcost switch footaccess 0 1000000
